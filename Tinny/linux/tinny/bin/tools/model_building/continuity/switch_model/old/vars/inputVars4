//
//************************************************
// Input variables
//************************************************
//
// INP
VAR  IS_AUTH : FLOAT
GRP = INP
RCOD = 10
CALC = 
STR_EQL(TRAN.AUTH_POST_FLAG, CON_A);

VAR  CURRENT_DATE : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   RETURN(Date_Convert(TRAN.TRAN_DATE));
ENDVAR;

VAR  CURRENT_TIME : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   RETURN( Time_Convert(TRAN.TRAN_TIME) );
ENDVAR;

VAR  CARD_OPNDATE_98S : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   IF(Valid_Date(TRAN.ACCOUNT_OPEN_DATE)) THEN
     BEGIN
       IF ( TRAN.ACCOUNT_OPEN_DATE < TRAN.TRAN_DATE ) THEN
         RETURN(Date_Convert(TRAN.ACCOUNT_OPEN_DATE));
       ELSE
         RETURN(CURRENT_DATE);
     END;
   ELSE IF (START_DATE_98S <> 0 ) THEN
      RETURN(START_DATE_98S );
   ELSE
      RETURN(CURRENT_DATE );
ENDVAR;

VAR  START_DATE_98S : FLOAT
GRP = INP
MAP = PROF.P222
RCOD = 0
CALC =
BEGIN
  IF (START_DATE_98S = 0) THEN
    BEGIN
      PROF.P6  := 0;
      PROF.P22 := 0;
      PROF.P30 := 0;
      PROF.P32 := 0;
      IF((CURRENT_DATE - CARD_OPNDATE_98S) > 90.0) THEN
         RETURN(CURRENT_DATE - 90.0);
      ELSE
      RETURN(CARD_OPNDATE_98S);
    END;
  ELSE
    RETURN(START_DATE_98S);
ENDVAR;

VAR  TMP_CURR_CONV : FLOAT
GRP = INP
RCOD = 0
CALC =
BEGIN
   IF ( TRAN.TRAN_CURR_CODE = "840" ) THEN
      RETURN(1.0);
   ELSE IF ( TRAN.TRAN_CURR_CODE = "   " ) THEN
      RETURN(CONV_TO_DOLLARS);
   ELSE IF ( TRAN.TRAN_CURR_CODE = "124" ) THEN
     BEGIN
       IF ( (Valid_Amount(TRAN.TRAN_CURR_CONV)) AND
          (TRAN.TRAN_CURR_CONV > 0.0) AND (TRAN.TRAN_CURR_CONV < 0.999) ) THEN
            RETURN(TRAN.TRAN_CURR_CONV);
       ELSE
            RETURN(0.67);
     END;
   ELSE IF ( (Valid_Amount(TRAN.TRAN_CURR_CONV)) AND
             (TRAN.TRAN_CURR_CONV > 0.0) ) THEN
      RETURN(TRAN.TRAN_CURR_CONV);
   ELSE
      RETURN(CONV_TO_DOLLARS);
ENDVAR;

VAR  IS_EXP_MISMATCH : FLOAT
GRP = INP
RCOD = 5
CALC = 
BEGIN
   IF (Is_Missing(TRAN.CARD_EXPIRE_DATE) OR Is_Missing(TRAN.ACCOUNT_EXPIRE_DATE)) THEN
      RETURN(0);
   ELSE RETURN(LEFT(TRAN.CARD_EXPIRE_DATE, 6) <> LEFT(TRAN.ACCOUNT_EXPIRE_DATE, 6));
ENDVAR;

VAR  CARD_ISSDATE : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   IF(Valid_Date(TRAN.NEW_PLASTIC_DATE)) THEN
     BEGIN
       IF ( TRAN.NEW_PLASTIC_DATE < TRAN.TRAN_DATE ) THEN
         RETURN(Date_Convert(TRAN.NEW_PLASTIC_DATE));
       ELSE
         RETURN(CURRENT_DATE);
     END;
   ELSE
      RETURN(CARD_OPNDATE_98S );
ENDVAR;

VAR  ACCT_AGE_98S : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   RETURN(CURRENT_DATE   - START_DATE_98S
          + (CURRENT_TIME   / DAY_SECS));
ENDVAR;

VAR  PLASTIC_AGE : FLOAT
GRP = INP
RCOD = 11
CALC = 
BEGIN
   RETURN( CURRENT_DATE   - CARD_ISSDATE  );
ENDVAR;

VAR  IS_ACCTAGE_LE_30D : FLOAT
GRP = INP
RCOD = 11
CALC = 
BEGIN
  RETURN( (CURRENT_DATE   - CARD_OPNDATE_98S  ) <= 35 );
ENDVAR;

VAR  CARD_CREDIT_LIMIT : FLOAT
GRP = INP
ALT = CRLINE
RCOD = 4
CALC = 
BEGIN
   IF (Is_Missing(TRAN.CREDIT_LINE) OR TRAN.CREDIT_LINE = 0.0) THEN
      RETURN(RECENT_CREDIT_LIMIT  );
   ELSE
      RETURN(TRAN.CREDIT_LINE * TMP_CURR_CONV);
ENDVAR;

VAR  LOG_DOL_AMT_GE_1K : FLOAT
GRP = INP
RCOD = 4
CALC = 
BEGIN
   IF ( DOL_AMT   >= 1000.0) THEN
     return( log10(DOL_AMT  /1000.0) );
   ELSE
     return( 0.0 );
ENDVAR;

VAR  IS_HI_AMT : FLOAT
GRP = INP
ALT = HID
RCOD = 4
CALC = 
FLOAT_GRTR(DOL_AMT, AUTH_HI_LIMIT);

VAR  IS_LO_AMT : FLOAT
GRP = INP
ALT = LOD
RCOD = 5
CALC = 
BEGIN
   return (DOL_AMT   <= AUTH_LO_LIMIT);
ENDVAR;

VAR  IS_ONE_DOL : FLOAT
GRP = INP
ALT = ONE_DOLL
RCOD = 5
CALC = 
BEGIN
   return ( (DOL_AMT >= 0.999 AND DOL_AMT <= 1.001) );
ENDVAR;

VAR  IS_GE_180_DOL : FLOAT
GRP = INP
RCOD = 5
CALC = 
FLOAT_GRTREQL(DOL_AMT, CON_179);

VAR  LOCAL_DATE_98S : FLOAT
GRP = INP
RCOD = 0
CALC = 
FLOAT_SET(CURRENT_DATE  );

VAR  AUTH_DELTA_TIME : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   RETURN((CURRENT_DATE   - AUTH_RECENT_UPDATE_DATE  ) +
          ((CURRENT_TIME   - AUTH_RECENT_UPDATE_TIME  ) / DAY_SECS));
ENDVAR;

VAR  AU_LAST_AGE_98S : FLOAT
GRP = INP
RCOD = 0
CALC = 
BEGIN
   RETURN(AUTH_RECENT_UPDATE_DATE - START_DATE_98S
          + (AUTH_RECENT_UPDATE_TIME / DAY_SECS));
ENDVAR;
 
VAR  CURRENT_SIC : FLOAT
GRP = INP
RCOD = 2
CALC =
BEGIN
   IF (IS_MISSING(TRAN.MERCH_CAT)) THEN RETURN(-1);
   ELSE RETURN(TRAN.MERCH_CAT);
ENDVAR;
